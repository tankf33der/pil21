#!/data/data/com.termux/files/home/pil21/pil
# 31dec21abu
# Pseudo Terminal (PilBox)

(load "@lib/term.l")

(unless (setq *Sock (connect (or (opt) "localhost") 8082))
   (bye) )
(out *Sock (in "~/.pty" (echo)))

(task (port 8083)
   (let? S (accept @)
      (catch '(NIL)
         (in S
            (when (= (rd) (in "~/.pty" (line T)))
               (casq (rd)
                  (+ (in (rd) (out S (echo))))
                  (- (out (rd) (echo))) ) ) )
         (off *Msg) )
      (close S)
      (and *Msg (prinl @)) ) )

(push '*Bye '(prinl))

(when (getTerm)
   (out *Sock
      (space)
      (println
         (cons 'setTerm "screen" @)
         '(off *Err) ) )
   (in *Sock (till "-") (till "\n")) )

(raw T)
(call "stty" "intr" NIL)

(task *Sock
   (in @
      (ifn (rd 1)
         (bye)
         (wr @)
         (flush) ) ) )

(loop
   (and (key) (out *Sock (prin @))) )
